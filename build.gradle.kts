var javaVersion = 17;
group = "com.github.webmorph"
version = "1.0.3"

plugins {
    id("java-library")
    id("maven-publish")
    id("io.spring.dependency-management").version("1.1.7")
    id("io.github.gradle-nexus.publish-plugin").version("1.1.0")
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}

configurations {
    compileOnly {
        extendsFrom(configurations.annotationProcessor.get())
    }
}

repositories {
    mavenCentral()
    maven("https://repo.jyraf.com/repository/maven-public/")
}

dependencies {
    // Spring
    api("org.springframework.boot:spring-boot-starter:3.5.0")

    // Logger
    api("com.github.webmorph:logger:1.0.1")

    // Permissions
    api("net.luckperms:standalone:5.5.5")

    // Mixin
    annotationProcessor("net.lenni0451.classtransform:mixinsdummy:1.14.1")
    api("net.lenni0451.classtransform:mixinsdummy:1.14.1")

    // Yaml
    api("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.19.0")

    // Lombok
    compileOnly("org.projectlombok:lombok:1.18.38")
    annotationProcessor("org.projectlombok:lombok:1.18.38")
}

tasks {
    register<Jar>("sourcesJar") {
        archiveClassifier.set("sources")
        from(sourceSets.main.get().allSource)
    }
    register<Jar>("javadocJar") {
        archiveClassifier.set("javadoc")
        from(javadoc)
    }
    javadoc {
        options.encoding = "UTF-8"
        options.memberLevel = JavadocMemberLevel.PUBLIC
        isFailOnError = false
    }
    withType<JavaCompile> {
        options.encoding = Charsets.UTF_8.name()
        options.release.set(javaVersion)
    }
    build {
        dependsOn("sourcesJar", "javadocJar")
    }
    jar {
        enabled = true
    }
}


publishing {
    publications {
        create<MavenPublication>("mavenJava") {
            from(components["java"])
            artifact(tasks["sourcesJar"])
            artifact(tasks["javadocJar"])
        }
    }
    repositories {
        maven {
            name = "jyrafRepo"
            url = uri("https://repo.jyraf.com/repository/maven-releases/")
            credentials {
                username = System.getenv("NEXUS_USERNAME")
                password = System.getenv("NEXUS_PASSWORD")
            }
        }
    }
}